apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.versions = [
        // Android
        targetSdkVersion    : 26,
        minSdkVersion       : 21,
        buildtools          : "26.0.2",
        support             : "27.0.0",

        // Components
        constraintlayout    : "1.1.0-beta3",

        // Logging
        timber              : "4.5.1",

        // UI
        calligraphy         : "2.3.0",

        // Utility
        butterknife         : "8.8.1",
        kotterknife         : "0.1.0-SNAPSHOT"
]

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0


android {
    compileSdkVersion versions.targetSdkVersion
    buildToolsVersion versions.buildtools
    defaultConfig {
        applicationId "com.randomlytyping.ccl"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation "com.android.support:support-v4:$versions.support"
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:support-annotations:$versions.support"

    // Components
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintlayout"
    implementation "com.android.support:cardview-v7:$versions.support"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // UI
    implementation "uk.co.chrisjenx:calligraphy:$versions.calligraphy"

    // Utility
    implementation "com.jakewharton:kotterknife:$versions.kotterknife"

    // Testing
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}
